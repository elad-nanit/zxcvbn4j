plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id "me.champeau.jmh" version "0.7.1"
    id "com.github.spotbugs" version "5.1.3"
    id 'com.github.sherter.google-java-format' version '0.9'
}

group = 'com.nulab-inc'
version = '1.9.0'
ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

base {
    archivesBaseName = 'zxcvbn'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

wrapper {
    gradleVersion = '8.3'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.graalvm.js', name: 'js', version: '22.0.0.2'
    testImplementation group: 'org.graalvm.js', name: 'js-scriptengine', version: '22.0.0.2'
    jmh group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.34'
    jmh group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.34'
    jmh group: 'org.ow2.asm', name: 'asm', version: '9.3'
}

spotbugs {
    ignoreFailures = false
    effort = 'max'
    reportLevel = 'high'
}

spotbugsTest {
    enabled = false
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle {
    toolVersion = '10.12.1'
    configProperties = [
            "org.checkstyle.google.suppressionxpathfilter.config":
                    "${projectDir}/config/checkstyle/suppressions-xpath.xml",
    ]
    ignoreFailures = false
    maxWarnings = 0
    checkstyleTest.enabled = false
}

task compileModuleInfoJava(type: JavaCompile, dependsOn: compileJava) {
    classpath = files()
    source = 'src/main/java9/module-info.java'
    destinationDirectory  = compileJava.destinationDirectory
    doLast {
        def descriptor = new File(destinationDirectory.get().asFile, 'module-info.class')
        def dest = new File(destinationDirectory.get().asFile, 'META-INF/versions/9')
        ant.move file: descriptor, todir: dest
    }

    doFirst {
        options.compilerArgs = [
                '--release', '9',
                '--module-path', compileJava.classpath.asPath
        ]
    }
 }

classes.dependsOn compileModuleInfoJava

javadoc {
    options.locale = 'en_US'
    title = 'zxcvbn4j ' + version + ' API'
}

// Turning off doclint in JDK 8 Javadoc
// ref : http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': base.archivesBaseName, 'Implementation-Version': archiveVersion, 'Multi-Release': true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

configurations {
    deployerJars
}

publishing {
    repositories {
        maven {
            // Skip uploading production archives when using github actions
            if (System.env.GITHUB_ACTIONS && isReleaseVersion) return

            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'Unknown user'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'Unknown password'
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'zxcvbn4j'
                artifactId = base.archivesBaseName
                packaging = 'jar'
                description = 'This is a java port of zxcvbn, which is a JavaScript password strength generator.'
                url = 'https://github.com/nulab/zxcvbn4j'

                scm {
                    connection = 'https://github.com/nulab/zxcvbn4j.git'
                    developerConnection = 'https://github.com/nulab/zxcvbn4j.git'
                    url = 'https://github.com/nulab/zxcvbn4j.git'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'vvatanabe'
                        name = 'Yuichi Watanabe'
                        email = 'yuichi.watanabe@nulab-inc.com'
                    }
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        // Skip signing snapshot archives
        if (System.env.GITHUB_ACTIONS || !isReleaseVersion) return

        sign publishing.publications.mavenJava
    }
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 4
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}